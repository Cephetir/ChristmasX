plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge" version "6f53277"
    id "org.spongepowered.mixin" version "d75e32e"
}

version = "1.0"
group = "me.cephetir"
archivesBaseName = "ChristmasX"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
}

configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    mavenCentral()
    maven { url = "https://repo.sk1er.club/repository/maven-public/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
}

dependencies {
    implementation('org.projectlombok:lombok:1.18.24')
    annotationProcessor('org.projectlombok:lombok:1.18.24')
    include('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
}

mixin {
    add sourceSets.main, "mixins.christmasx.refmap.json"
    defaultObfuscationEnv = searge
}

jar {
    enabled = false
}

sourceJar {
    enabled = false
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

shadowJar {
    archiveFileName = jar.archiveFileName

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    configurations = [project.configurations.include]

    manifest.attributes(
            "ForceLoadAsMod": true,
            'ModSide': 'CLIENT',
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            'MixinConfigs': "mixins.christmasx.json",
    )
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

reobfJar.dependsOn shadowJar
