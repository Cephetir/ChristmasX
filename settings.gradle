pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()

        // Add the Forge Repository (ForgeGradle fetches most of its stuff from here)
        maven {
            name = "Forge"
            url = "https://maven.minecraftforge.net"
        }

        // Add the Jitpack Repository (We fetch ForgeGradle from this)
        maven {
            name = "Jitpack"
            url = "https://jitpack.io/"
        }

        maven { url = "https://repo.spongepowered.org/maven" }
    }
    resolutionStrategy {
        eachPlugin {
            // If the "net.minecraftforge.gradle.forge" plugin is requested we redirect it to asbyth's ForgeGradle fork
            if (requested.id.id == "net.minecraftforge.gradle.forge") {
                useModule("com.github.asbyth:ForgeGradle:${requested.version}")
            }
            if (requested.id.id == "org.spongepowered.mixin") {
                useModule("com.github.LxGaming:MixinGradle:${requested.version}")
            }
        }
    }
}

rootProject.name = "V0ID"